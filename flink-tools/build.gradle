/*
 * Copyright (c) Dell Inc., or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 */

// Based on https://ci.apache.org/projects/flink/flink-docs-release-1.9/dev/projectsetup/java_api_quickstart.html

apply plugin: "java"
apply plugin: "maven"
apply plugin: "application"
apply plugin: "maven-publish"
apply plugin: "com.github.johnrengelman.shadow"

group = "io.pravega"
archivesBaseName = "pravega-flink-tools"
description = "Pravega Flink Tools"
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Allow main class name to be specified with -PmainClass=xxx.
application {
    mainClassName = project.hasProperty("mainClass") ? getProperty("mainClass") : "io.pravega.flinktools.Main"
}

// NOTE: We cannot use "compileOnly" or "shadow" configurations since then we could not run code
// in the IDE or with "gradle run". We also cannot exclude transitive dependencies from the
// shadowJar yet (see https://github.com/johnrengelman/shadow/issues/159).
// Explicitly define the libraries we want to be included in the "flinkShadowJar" configuration!
configurations {
    flinkShadowJar // dependencies which go into the shadowJar

    // always exclude these (also from transitive dependencies) since they are provided by Flink
    flinkShadowJar.exclude group: 'org.apache.flink', module: 'force-shading'
    flinkShadowJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
    flinkShadowJar.exclude group: 'org.slf4j'
    flinkShadowJar.exclude group: 'log4j'
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.20'
    implementation 'org.projectlombok:lombok:1.18.20'

    // --------------------------------------------------------------
    // Compile-time dependencies that should NOT be part of the
    // shadow jar and are provided in the lib folder of Flink
    // --------------------------------------------------------------
    compile "org.apache.flink:flink-java:${flinkVersion}"
    compile "org.apache.flink:flink-streaming-java_${flinkScalaVersion}:${flinkVersion}"
    compile "org.apache.flink:flink-table-api-java-bridge_${flinkScalaVersion}:${flinkVersion}"
    compile "org.apache.flink:flink-table-planner_${flinkScalaVersion}:${flinkVersion}"
    compile "org.apache.flink:flink-table-planner-blink_${flinkScalaVersion}:${flinkVersion}"



    // --------------------------------------------------------------
    // Dependencies that should be part of the shadow jar, e.g.
    // connectors. These must be in the flinkShadowJar configuration!
    // --------------------------------------------------------------

    flinkShadowJar project(':flink-parquet')
    flinkShadowJar "io.pravega:pravega-connectors-flink-${flinkShortVersion}_${flinkScalaVersion}:${pravegaFlinkConnectorVersion}"
    flinkShadowJar "com.github.vladimir-bukhtoyarov:bucket4j-core:${bucket4jVersion}"
    flinkShadowJar "com.google.guava:guava:${guavaVersion}"
    flinkShadowJar "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    flinkShadowJar "org.apache.flink:flink-avro:${flinkVersion}"
    flinkShadowJar "org.apache.flink:flink-parquet_${flinkScalaVersion}:${flinkVersion}"
    flinkShadowJar "org.apache.parquet:parquet-avro:${parquetVersion}"
    flinkShadowJar "org.apache.avro:avro:${avroVersion}"
    // Also include Avro in compile list so that it gets added to classpath before old version from Hadoop.
    compile        "org.apache.avro:avro:${avroVersion}"

    if (includePravegaCredentials.toBoolean()) {
        flinkShadowJar "io.pravega:pravega-keycloak-client:${pravegaCredentialsVersion}"
    }

    // Schema registry dependencies required due to https://github.com/pravega/flink-connectors/issues/440
    flinkShadowJar group: 'io.pravega', name: 'schemaregistry-serializers', classifier: 'all', version: schemaRegistryVersion

    if (includeHadoopS3.toBoolean()) {
        compile "org.apache.flink:flink-s3-fs-hadoop:${flinkVersion}"
        compile "com.amazonaws:aws-java-sdk:${awsSDKVersion}"
        compile "org.apache.hadoop:hadoop-client:${hadoopVersion}"
    }

    compile "org.slf4j:slf4j-api:${slf4jApiVersion}"
    compile "ch.qos.logback:logback-classic:${logbackVersion}"
    compile "ch.qos.logback:logback-core:${logbackVersion}"
    compile "org.scala-lang:scala-compiler:${scalaCompilerVersion}"

    testCompile "org.apache.flink:flink-test-utils_${flinkScalaVersion}:${flinkVersion}"
    testCompile "junit:junit:${junitVersion}"
}

// make compileOnly dependencies available for tests:
sourceSets {
    main.compileClasspath += configurations.flinkShadowJar
    main.runtimeClasspath += configurations.flinkShadowJar

    test.compileClasspath += configurations.flinkShadowJar
    test.runtimeClasspath += configurations.flinkShadowJar

    javadoc.classpath += configurations.flinkShadowJar
}

run.classpath = sourceSets.main.runtimeClasspath

jar {
    manifest {
        attributes 'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version')
    }
}

shadowJar {
    configurations = [project.configurations.flinkShadowJar]
}

publishing {
    repositories {
        maven {
            credentials {
                username "$System.env.MAVEN_USERNAME"
                password "$System.env.MAVEN_PASSWORD"
            }
            url "$System.env.MAVEN_URL"
        }
    }

    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}
